package(default_visibility = ["//visibility:public"])

load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("//npm:package.bzl", "enhanced_npm_package")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")
load("@npm//@bazel/jasmine:index.bzl", "jasmine_node_test")

all_sources = ["**/*.ts"]

all_tests = [
    "**/*.spec.ts",
    "**/test/**",
]

lib_name = "lockfile-filter"

module_name = "@djabry/" + lib_name

npm_deps = [
    "@npm//ts-stream",
    "@npm//yargs",
]

ts_library(
    name = lib_name,
    srcs = glob(
        all_sources,
        exclude = all_tests,
    ),
    module_name = module_name,
    deps = [
        "@npm//@types/node",
        "@npm//@types/yargs",
    ] + npm_deps,
)

ts_library(
    name = "test_lib",
    srcs = glob(all_tests),
    deps = [
        lib_name,
        "@npm//@types/chai",
        "@npm//@types/jasmine",
        "@npm//@types/node",
        "@npm//chai",
    ],
)

jasmine_node_test(
    name = "test",
    deps = [
        ":test-yarn.lock",
        ":test_lib",
        "@npm//chai",
        "@npm//jasmine",
    ],
)

nodejs_binary(
    name = "bin",
    data = [
        lib_name,
        "@npm//source-map-support",
    ],
    entry_point = "cli.ts",
)

enhanced_npm_package(
    name = "package",
    srcs = [
        "README.md",
    ],
    module_name = module_name,
    npm_deps = npm_deps,
    package_layers = [
        "//:common.package.json",
        "template.package.json",
    ],
    root_package_json = "//:package.json",
    deps = [
        lib_name,
    ],
)
